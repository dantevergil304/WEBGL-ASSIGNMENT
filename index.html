<!DOCTYPE html>
<html>
<head>
	<title>First step with WEBGL</title>
	<meta charset="utf-8">
	<script src="./lib/glMatrix.js"></script>
	<script src="./lib/webgl-utils.js"></script>

	<script id="shadow-fs" type="x-shader/x-fragment">
	    precision mediump float;
	    void main(void) {
				const vec4 bitShift = vec4(1.0, 256.0, 256.0 * 256.0, 256.0 * 256.0 * 256.0);
				const vec4 bitMask = vec4(1.0/256.0, 1.0/256.0, 1.0/256.0, 0.0);
				vec4 rgbaDepth = fract(gl_FragCoord.z * bitShift);
				rgbaDepth -= rgbaDepth.gbaa * bitMask;
	      gl_FragColor = rgbaDepth;
	    }
	</script>

	<script id="shadow-vs" type="x-shader/x-vertex">
	    attribute vec3 aVertexPosition;

	    uniform mat4 uMVMatrix;
	    uniform mat4 uPMatrix;
	    void main(void) {
	      gl_Position = uPMatrix * uMVMatrix * vec4(aVertexPosition, 1.0);
	    }
	</script>

	<script id="shader-fs" type="x-shader/x-fragment">
			precision mediump float;

			varying vec2 vTextureCoord;
			varying vec3 vTransformedNormal;
			varying vec4 vPosition;
			varying vec4 vPositionFromLight;

			uniform vec3 uAmbientColor;

			uniform vec3 uPointLightingLocation;
			uniform vec3 uPointLightingColor;

			uniform sampler2D uSampler;
			uniform sampler2D uShadowMap;

			float unpackDepth(const in vec4 rgbaDepth) {
				const vec4 bitShift = vec4(1.0, 1.0/256.0, 1.0/(256.0 * 256.0), 1.0/(256.0 * 256.0 * 256.0));
				float depth = dot(rgbaDepth, bitShift);
				return depth;
			}

			void main(void) {
					vec3 lightWeighting;
					vec3 lightDirection = normalize(uPointLightingLocation - vPosition.xyz);

					float directionalLightWeighting = max(dot(normalize(vTransformedNormal), lightDirection), 0.0);
					lightWeighting = uAmbientColor + uPointLightingColor * directionalLightWeighting;

					vec3 shadowCoord = (vPositionFromLight.xyz / vPositionFromLight.w)/2.0 + 0.5;
					vec4 rgbaDepth = texture2D(uShadowMap, shadowCoord.xy);
					float depth = unpackDepth(rgbaDepth);
					float visibility = (shadowCoord.z > depth + 0.00004) ? 0.6 : 1.0;

					vec4 fragmentColor;
					fragmentColor = texture2D(uSampler, vec2(vTextureCoord.s, vTextureCoord.t));
					gl_FragColor = vec4(fragmentColor.rgb * lightWeighting * visibility, fragmentColor.a);
			}
	</script>

	<script id="shader-vs" type="x-shader/x-vertex">
			attribute vec3 aVertexPosition;
			attribute vec3 aVertexNormal;
			attribute vec2 aTextureCoord;

			uniform mat4 uMVMatrix;
			uniform mat4 uPMatrix;
			uniform mat3 uNMatrix;
			uniform mat4 uMVMatrixFromLight;
			uniform mat4 uPMatrixFromLight;

			varying vec2 vTextureCoord;
			varying vec3 vTransformedNormal;
			varying vec4 vPosition;
			varying vec4 vPositionFromLight;

			void main(void) {
					vPositionFromLight = uPMatrixFromLight * uMVMatrixFromLight * vec4(aVertexPosition, 1.0);
					vPosition = uMVMatrix * vec4(aVertexPosition, 1.0);
					gl_Position = uPMatrix * vPosition;
					vTextureCoord = aTextureCoord;
					vTransformedNormal = uNMatrix * aVertexNormal;
			}
	</script>
	<script src="./src/Lighting.js"></script>
	<script src="./src/Wall.js"></script>
	<script src="./src/Flat.js"></script>
	<script src="./src/Cube.js"></script>
	<script src="./src/Sphere.js"></script>
	<script src="./src/script.js"></script>
	<style>
		#text{
			position: absolute;
			left: 400px;
			top: 0px;
			z-index: 10;
		}
	</style>
</head>
<body>
	<canvas id="text" width="100" height="100"></canvas>
	<canvas id='canvas' width='900' height='600'></canvas>
</body>
</html>
